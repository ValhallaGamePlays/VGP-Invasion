//Q3 Phobos
//Weapon: Rocket Launcher
//Behavior is based on this: realm667.com/index.php/en/component/docman/?task=doc_download&gid=807&Itemid=

ACTOR Q3Phobos 27028
{
  //$Category VGPINV08 Source
  //$Sprite PHOBA1
  //$Title "Q3 Phobos"
  obituary "%o ate Phobos' rocket"
  health 200
  radius 20
  height 56
  mass 100
  speed 12
  Translation "112:127=216:223"
  painchance 100
  seesound "Phobos/sight"
  deathsound "Phobos/death"
  dropitem "RocketBox" 256
  MONSTER
  +FLOORCLIP
  +NORADIUSDMG
  +DONTMORPH
  +MISSILEMORE  
  states
  {
  Spawn:
    PHOB AB 10 A_Look
    loop
  See:
    PHOB AABBCCDD 2 A_Chase
    PHOB A 0 A_jump(80,2)
    loop
    PHOB AABBCCDDAABBCCDD 2 A_FastChase
    Goto See  
  Missile:
	PHOB E 8
	PHOB F 6 BRIGHT A_CustomMissile ("PhobosRocket")	//some custom missile might come here
	PHOB E 6
	PHOB A 0 A_CPosRefire
	Loop
	PHOB A 0
	Goto See
  Pain:
    PHOB A 0 A_JumpIfHealthLower (75, "Pain75")
    PHOB G 3 A_PlaySound("Phobos/Pain100",5)
    goto See
  Pain75:
    PHOB A 0 A_JumpIfHealthLower (50, "Pain50")
    PHOB G 3 A_PlaySound("Phobos/Pain75",5)
    goto See
  Pain50:
    PHOB A 0 A_JumpIfHealthLower (25, "Pain25")
    PHOB G 3 A_PlaySound("Phobos/Pain50",5)
    goto See
  Pain25:
    PHOB G 3 A_PlaySound("Phobos/Pain25",5)
    goto See
  Death:
    PHOB H 5
    PHOB I 5 A_Scream
    PHOB J 5 A_NoBlocking
    PHOB K 5
    PHOB LM 5
    PHOB N -1
    stop
  Xdeath:
    	PHOB O 5
		PHOB P 5 A_XScream
		PHOB Q 5 A_NoBlocking
		PHOB RSTUV 5
		PHOB W -1
		stop
  }
}

ACTOR PhobosRocket : Rocket
{
	Damage 64
	explosiondamage 16
	Explosionradius 64
}


actor Q3PhobosSpot : CustomMonsterInvasionSpot 27029
{
	//$Category VGPINV08 Source
	//$Arg0 "Start Spawn Number"
	//$Arg1 "Spawn Delay"
	//$Arg2 "Round Spawn Delay"
	//$Arg3 "First Appear Wave"
	//$Arg4 "Max Spawn"
	dropitem Q3Phobos
}