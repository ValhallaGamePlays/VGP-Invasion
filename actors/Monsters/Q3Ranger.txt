//Q3Ranger
//Weapon: Lightning Gun
//Behavior is based on this: realm667.com/index.php/en/component/docman/?task=doc_download&gid=807&Itemid=

ACTOR Q3Ranger 27030
{
  //$Category VGPINV08 Source
  //$Sprite QGUYA1
  //$Title "Q3 Ranger"
  obituary "%o was electrocuted by Ranger"
  health 500
  radius 20
  height 56
  mass 100
  speed 12
  Translation "112:127=64:79"
  painchance 100
  seesound "ranger/sight"
  deathsound "ranger/death"
  AttackSound "ranger/attack"
  dropitem "Cellpack" 256
  MONSTER
  +FLOORCLIP
  +NORADIUSDMG
  +DONTMORPH
  +MISSILEMORE  
  states
  {
  Spawn:
    QGUY AB 10 A_Look
    loop
  See:
    QGUY AABBCCDD 2 A_Chase
    QGUY A 0 A_jump(80,2)
    loop
    QGUY AABBCCDDAABBCCDD 2 A_FastChase
    Goto See  
  Missile:
    QGUY E 4 A_FaceTarget
	QGUY F 0 A_PlaySound("ranger/LightningStart")
	GoTo LGBolt
   LGBolt:
    TNT1 A 0 A_Jump(16,"LGEnd")
	QGUY F 0 A_PlaySound("ranger/LightningLoop", CHAN_WEAPON)
	QGUY F 8 Light(WhiteBigFlicker3) A_Customrailgun(random(8,20),7,"none","LightBlue",3,1,2)
	QGUY F 0 A_CPosRefire
	Loop
   LGEnd:
    TNT1 A 0 A_StopSound(6)
	QGUY E 7
	Goto See
  Pain:
    QGUY A 0 A_JumpIfHealthLower (75, "Pain75")
    QGUY G 3 A_PlaySound("ranger/Pain100",5)
    goto See
  Pain75:
    QGUY A 0 A_JumpIfHealthLower (50, "Pain50")
    QGUY G 3 A_PlaySound("ranger/Pain75",5)
    goto See
  Pain50:
    QGUY A 0 A_JumpIfHealthLower (25, "Pain25")
    QGUY G 3 A_PlaySound("ranger/Pain50",5)
    goto See
  Pain25:
    QGUY G 3 A_PlaySound("ranger/Pain25",5)
    goto See
  Death:
    QGUY H 5
    QGUY I 5 A_Scream
    QGUY J 5 A_NoBlocking
    QGUY K 5
    QGUY LM 5
    QGUY N -1
    stop
  Xdeath:
    		QGUY O 5
		QGUY P 5 A_XScream
		QGUY Q 5 A_NoBlocking
		QGUY RSTUV 5
		QGUY W -1
		stop
  }
}

actor QuakeGrenade
{
PROJECTILE
BounceType "Doom"
//+GRENADETRAIL
+CANBOUNCEWATER
-NOGRAVITY
+EXTREMEDEATH
+FORCERADIUSDMG
Radius 5
Height 8
Speed 18
Gravity 0.4
Damage 20
BounceFactor 0.6
BounceSound "weapons/grenadebounce"
Obituary "%o was shredded by Ranger's shrapnel"
Scale 0.4
States
{
Spawn:
	TNT1 A 0
	TNT1 A 0 ThrustThingZ(0,7,0,1)
	TNT1 A 0 A_StopSound
	QGRP KJIHGFEDCBA 3 Bright A_SpawnItemEx("QuakeGrenSmokeTrail",random(0,-3),random(2,-2),random(2,-2),0,0,0.8,0,32)
	Goto Spawn+3
Death:
	TNT1 A 0 A_PlaySound("weapons/rockexplo",0.6)
	//TNT1 A 0 A_QuakeEx(3,3,0, 15,0,5,"", QF_SCALEDOWN)
	TNT1 A 0 A_Explode(random(90,166),128,1)
	TNT1 A 0 A_SpawnItemEx("ExplosionMediumBigSprite",0,0,0,0,0,0,0,32)
	Stop
}
}

Actor QuakeGrenSmokeTrail
{
+NOINTERACTION
Scale 0.15
States
{
Spawn:
	TNT1 A 0
	TNT1 A 0 A_Jump(210,3,5,7,9)
	X124 ABCDEFGHIJKLMNOPQR 2 A_FadeOut(0.03)
	Goto Spawn+2
}
}
Actor QuakeGrenSmokeTrailBig : QuakeGrenSmokeTrail
{
	Scale 0.24
}

actor Q3RangerSpot : CustomMonsterInvasionSpot 27031
{
	//$Category VGPINV08 Source
	//$Arg0 "Start Spawn Number"
	//$Arg1 "Spawn Delay"
	//$Arg2 "Round Spawn Delay"
	//$Arg3 "First Appear Wave"
	//$Arg4 "Max Spawn"
	dropitem Q3Ranger
}